#version 410 core

layout (quads, fractional_even_spacing) in;

in TCS_OUT {
	vec2 tc;
} tes_in[];

uniform mat4 crop;
uniform mat4 view;
uniform mat4 projection;
uniform float scale = 1.0;

uniform sampler2D height_map;

void main()
{
	//interpolate the 4 height map coordinates
	vec2 tc1 = mix(tes_in[0].tc, tes_in[1].tc, gl_TessCoord.x);
	vec2 tc2 = mix(tes_in[2].tc, tes_in[3].tc, gl_TessCoord.x);
	vec2 tc = mix(tc2, tc1, gl_TessCoord.y);

	//interpolate the position and add the height value
	vec4 p1 = mix(gl_in[0].gl_Position, gl_in[1].gl_Position, gl_TessCoord.x);
	vec4 p2 = mix(gl_in[2].gl_Position, gl_in[3].gl_Position, gl_TessCoord.x);
	vec4 pos = mix(p2, p1, gl_TessCoord.y);
	pos.y += texture(height_map, tc).r * scale;
	pos.w = 1.0;
	gl_Position = crop * pos;
}