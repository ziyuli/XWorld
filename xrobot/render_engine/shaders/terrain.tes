#version 410 core

layout (quads, fractional_even_spacing) in;

in TCS_OUT {
	vec2 tc;
} tes_in[];

out TES_OUT {
	vec2 tc;
	vec3 world;
	vec3 eye;
} tes_out;

uniform mat4 view;
uniform mat4 projection;
uniform float scale = 1.0;

uniform sampler2D height_map;

void main()
{
	//interpolate the 4 height map coordinates
	vec2 tc01 = mix(tes_in[0].tc, tes_in[1].tc, gl_TessCoord.x);
	vec2 tc23 = mix(tes_in[2].tc, tes_in[3].tc, gl_TessCoord.x);
	vec2 tc = mix(tc23, tc01, gl_TessCoord.y);

	//interpolate the position and add the height value
	vec4 p01 = mix(gl_in[0].gl_Position, gl_in[1].gl_Position, gl_TessCoord.x);
	vec4 p23 = mix(gl_in[2].gl_Position, gl_in[3].gl_Position, gl_TessCoord.x);
	vec4 pos = mix(p23, p01, gl_TessCoord.y);
	pos.y += texture(height_map, tc).r * scale;

	tes_out.tc = tc;
	tes_out.world = pos.xyz;

	pos.w = 1.0;

	vec4 eye = view * pos;
	tes_out.eye = eye.xyz;
	gl_Position = projection * eye;
}